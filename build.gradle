//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
    id "dev.architectury.loom" version '1.7.+'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

version = "$project.baseVersion+$project.branch"
archivesBaseName = "placeholder-api"
group = "eu.pb4"

repositories {
    maven { url "https://maven.nucleoid.xyz" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.neoforged.net/releases/" }
    maven { url "https://maven.su5ed.dev/releases" }
}

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.compileClasspath
    }
}

loom {
    runs {
        testmodClient {
            client()
            ideConfigGenerated project.rootProject == project
            name = "Test Mod Client"
            source sourceSets.testmod
        }
        testmodServer {
            server()
            ideConfigGenerated project.rootProject == project
            name = "Test Mod Server"
            source sourceSets.testmod
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fl_version}"
    modCompileOnly("net.fabricmc.fabric-api:fabric-api:${project.fapi_version}")
    modLocalRuntime("net.fabricmc.fabric-api:fabric-api:${project.fapi_version}")
    testmodImplementation sourceSets.main.output
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            version    : version,
            mc         : minecraft_version,
            fl         : fl_version,
            id         : meta_id,
            name       : meta_name,
            homepage   : meta_homepage,
            issues     : meta_issues,
            sources    : meta_sources,
            license    : meta_license,
            authors    : meta_authors.split(", ").collect {a -> "\"${a}\""}.toString().replace("[\"", "").replace("\"]", ""),
            description: meta_description
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
